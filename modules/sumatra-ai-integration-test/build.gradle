/*
 * Copyright (c) 2009 - 2021, DHBW Mannheim - TIGERs Mannheim
 */

plugins {
    id 'sumatra.java'
}

sourceSets {
    integrationTest {
        java.srcDir "src/integrationTest/java"
    }
}

dependencies {
    integrationTestImplementation project(':common')
    integrationTestImplementation project(':common-math')
    integrationTestImplementation project(':common-bot')
    integrationTestImplementation project(':sumatra-simulation-control')
    integrationTestImplementation project(':sumatra-snapshot')
    integrationTestImplementation project(':sumatra-model')
    integrationTestImplementation project(':sumatra-skillsystem')
    integrationTestImplementation project(':moduli-ai')
    integrationTestImplementation project(':moduli-autoreferee')
    integrationTestImplementation project(':moduli-geometry')
    integrationTestImplementation project(':moduli-referee')
    integrationTestImplementation project(':moduli-wp')
    integrationTestImplementation project(':moduli-record')
    integrationTestImplementation project(':moduli-ai-controller')
    integrationTestImplementation project(':moduli-bot-params')
    integrationTestRuntimeOnly project(':moduli-botmanager-sim')

    integrationTestImplementation 'com.github.g3force:configurable:3.0'

    integrationTestImplementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    integrationTestImplementation 'org.apache.logging.log4j:log4j-core:2.20.0'

    integrationTestImplementation 'junit:junit:4.13.2'
    integrationTestRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.9.3'
    integrationTestImplementation 'org.assertj:assertj-core:3.24.2'

    integrationTestAnnotationProcessor "org.projectlombok:lombok:1.18.28"
    integrationTestImplementation "org.projectlombok:lombok:1.18.28"

    integrationTestImplementation 'org.openjdk.jmh:jmh-core:1.36'
    integrationTestAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
}

tasks.register('integrationTest', Test) {
    description = "Run the integration tests"
    group = "verification"

    maxHeapSize = "2g"
    jvmArgs([
            '-server',
    ])

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    inputs.dir(file('config'))
}

tasks.register('runPathPlanningBenchmark', JavaExec) {
    group = "Execution"
    description = "Run PathPlanningPerfTest"
    classpath = sourceSets.integrationTest.runtimeClasspath
    mainClass.set("edu.tigers.sumatra.ai.integration.jmh.PathPlanningPerfTest")
    args("100")
}
